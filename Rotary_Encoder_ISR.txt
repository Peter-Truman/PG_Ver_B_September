ISR_Handler:
    Context Save
    Clrwdt

    If INTCONbits_T0IF = 1 Then
        TMR0L = 6
        INTCONbits_T0IF = 0

        Inc B_RE_Count
        If B_RE_Count > 9 Then
            Dim B_NewA  As Byte
            Dim B_NewB  As Byte
            Dim B_NewBtn As Byte

            B_NewA   = _ENC_A
            B_NewB   = _ENC_B
            B_NewBtn = _ENC_SW

            ' Debounce A
            If B_NewA <> B_AState Then
                Inc B_DebA
                If B_DebA >= 2 Then
                    B_AState = B_NewA
                    B_DebA = 0
                EndIf
            Else
                B_DebA = 0
            EndIf

            ' Debounce B
            If B_NewB <> B_BState Then
                Inc B_DebB
                If B_DebB >= 2 Then
                    B_BState = B_NewB
                    B_DebB = 0
                EndIf
            Else
                B_DebB = 0
            EndIf

            ' Debounce Button + reset inactivity
            If B_NewBtn <> B_ButtonState Then
                Inc B_DebBtn
                If B_DebBtn >= 2 Then
                    B_ButtonState   = B_NewBtn
                    L_TimeoutRemain = B_Menu_Timeout * 1000
                    b_MTimeout      = 0
                    B_DebBtn        = 0
                EndIf
            Else
                B_DebBtn = 0
            EndIf

            ' Quadrature
            Dim B_Curr As Byte
            B_Curr = (B_AState * 2) + B_BState

            Dim B_Combined As Byte
            B_Combined = (B_LastState * 4) + B_Curr

            Select B_Combined
                Case %0001, %0111, %1110, %1000
                    Dec S_Qacc
                Case %0010, %1011, %1101, %0100
                    Inc S_Qacc
            EndSelect

            If B_Curr = 0 Then
                If S_Qacc >= 2 Then
                    Inc W_EncoderPos
                    L_TimeoutRemain = B_Menu_Timeout * 1000
                    b_MTimeout = 0
                ElseIf S_Qacc <= -2 Then
                    Dec W_EncoderPos
                    L_TimeoutRemain = B_Menu_Timeout * 1000
                    b_MTimeout = 0
                EndIf
                S_Qacc = 0
            EndIf

            B_LastState = B_Curr
            Clear B_RE_Count
        EndIf
    EndIf

    ' Buzzer one-shot
    If B_BeepLen > 0 Then
        High _BUZZER
        Dec B_BeepLen
    Else
        Low _BUZZER
    EndIf

    ' Menu inactivity
    If L_TimeoutRemain > 0 Then
        Dec L_TimeoutRemain
        If L_TimeoutRemain = 0 Then
            Set b_MTimeout
        EndIf
    EndIf

    ' Long / very-long press
    If B_ButtonState = 0 Then
        If W_BtnHoldMS < 65535 Then
            Inc W_BtnHoldMS
        EndIf
        If W_BtnHoldMS >= LONG_MS Then
            If b_Long = 0 Then
                Set b_Long
                B_BeepLen = 200
            EndIf
            If W_BtnHoldMS >= VERY_LONG_MS Then
                If b_VLong = 0 Then
                    Set b_VLong
                    B_BeepLen = 255
                EndIf
            EndIf
        EndIf
    Else
        W_BtnHoldMS = 0
    EndIf

    Context Restore
over_Interrupt:
